dnl Process this file with autoconf to produce a configure script.

AC_INIT(create_mantle.cc)
AM_INIT_AUTOMAKE(laplacian_thickness, 0.0)
AM_CONFIG_HEADER(config.h)

# If prefix is set, use it to set CPPFLAGS and LDFLAGS.
# This helps when probing for libraries, later on, in the common case
# that the 3rd party libraries are also installed in the place where
# this package is going.
#
if test "$prefix" != "NONE"; then
    if test "$CPPFLAGS" = "" -a -d "${prefix}/include"; then
        CPPFLAGS="-I${prefix}/include"
	# volume_io is weird; we'll hack it for now
        test -d "${prefix}/include/volume_io" && \
		CPPFLAGS="$CPPFLAGS -I${prefix}/include/volume_io"
    fi
    if test "$LDFLAGS" = "" -a -d "${prefix}/lib"; then
        LDFLAGS="-L${prefix}/lib"
    fi
fi



#------------------------------------------------------------------------
# Checks for compiler toolset.
#------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

#------------------------------------------------------------------------
# Look for perl (and make sure it's the right version)
#------------------------------------------------------------------------

AC_PATH_PROGS(PERL, [perl5 perl], no)

if test "$PERL" = no; then
    AC_MSG_WARN(No perl program found!)
    PERL=
dnl else check perl version ....
fi


#------------------------------------------------------------------------
# Check for third-party libraries.
#------------------------------------------------------------------------

smr_CHECK_LIB(netcdf,,
[NetCDF library], ncopen, netcdf.h)
if test "$netcdf_LIBS" = ""; then
    AC_MSG_ERROR([Required library NetCDF not found!])
fi

smr_CHECK_LIB(minc,,
[MNI medical image library], miicv_create, minc.h, 
$netcdf_LIBS, $netcdf_CFLAGS)
if test "$minc_LIBS" = ""; then
    AC_MSG_ERROR([Required library minc not found!])
fi

smr_CHECK_LIB(volumeio,volume_io,
[MNI Volume IO library], input_volume, volume_io.h,
[$minc_LIBS $netcdf_LIBS -lm],[$netcdf_CFLAGS $minc_CFLAGS])
if test "$volumeio_LIBS" = ""; then
    AC_MSG_ERROR([Required library volume_io not found!])
fi

smr_CHECK_LIB(bicpl,,
[BIC Programming library], input_graphics_file, bicpl.h,
[$volumeio_LIBS $minc_LIBS $netcdf_LIBS -lm],[$netcdf_CFLAGS $minc_CFLAGS $volumeio_CFLAGS])
if test "$bicpl_LIBS" = ""; then
    AC_MSG_ERROR([Required library bicpl not found!])
fi

MNI_INCLUDES="$bicpl_CFLAGS $volumeio_CFLAGS $minc_CFLAGS $netcdf_CFLAGS"
MNI_LIBS="$bicpl_LIBS $volumeio_LIBS $minc_LIBS $netcdf_LIBS"

AC_SUBST(MNI_INCLUDES)
AC_SUBST(MNI_LIBS)


#------------------------------------------------------------------------
# Check that we have some required header files (and macros in them)
#------------------------------------------------------------------------
AC_HEADER_STDC
AC_CHECK_HEADERS(float.h limits.h malloc.h)


#------------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
#------------------------------------------------------------------------
AC_C_INLINE
AC_C_CONST
AC_TYPE_SIZE_T
#AC_CHECK_TYPE(bool,int)


#------------------------------------------------------------------------
# Checks for library functions.
#------------------------------------------------------------------------
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strtod strtol)


AC_OUTPUT(Makefile)
