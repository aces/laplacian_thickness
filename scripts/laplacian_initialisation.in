#!@PERL@
#
# Copyright Alan C. Evans
# Professor of Neurology
# McGill University
#

# creates the initial grid for solving using the laplacian thickness
# method

use warnings "all";
use strict;
use FindBin;

use Getopt::Tabular;
use MNI::Spawn;
use MNI::Startup;
use MNI::FileUtilities qw(test_file check_output_dirs);
use MNI::DataDir;

my ($stepValue, $icbmModel);
my ($inputVolume, $grey, $white, $outputFile);
my ($tmpVolume);
my ($help, $usage, $license);
my (@mincinfoOutput);

&Initialise;

# use one of the icbm models as the input volume
if ($icbmModel) {
  my $ModelDir = "$FindBin::Bin/../share/ICBM/";
  #my $ModelDir = MNI::DataDir::dir('ICBM');
  my @files = ("icbm_template_${icbmModel}mm.mnc");
  MNI::DataDir::check_data($ModelDir, \@files);
  $tmpVolume = "$ModelDir/icbm_template_${icbmModel}mm.mnc";

}
# otherwise resample the input file if desired
elsif ($stepValue and $inputVolume) {
    # find the current step sizes and dimlengths
    my @tmp;
    Spawn(['mincinfo', $inputVolume], verbose => 0, stdout => \@mincinfoOutput);
    for (my $i = 5; $i <= 7; $i++) {
	@tmp = split /\s+/, $mincinfoOutput[$i];

	# get the dimension letter (i.e. x y z)
	my $dim = $tmp[1];
	$dim =~ s/(\w).+/$1/;

	# get the number of elements needed
	my $nelements = sprintf("%d", ($tmp[2] * $tmp[3]) / $stepValue);

	# now add the args
	AddDefaultArgs('mincresample', ["-${dim}nelements", $nelements,
		       "-${dim}step", $stepValue], 'pre');

    }
    Spawn(['mincresample', $inputVolume, $tmpVolume]);
}
# or just take the input file as is
elsif ($inputVolume) {
    $tmpVolume = $inputVolume;
}
# or die if none of the above matched
else {
  die "ERROR: you must specify either -like <filename> or -icbm_model <sampling>\n";
}

# now generate the grid
Spawn(['create_laplacian_grid', $tmpVolume, $grey, $white, $outputFile]);

sub Initialise {

    # make sure necessary applications exist
    RegisterPrograms([qw(mincresample create_laplacian_grid mincinfo)]);

    &createInfoText;

    # defaults:
    
    my @leftOverArgs;
    my @argTbl = 
	(@DefaultArgs,
	 ["Programme specific options:", "section"],
     ["-like", "string", 1, \$inputVolume,
      "Volume to base grid on. Can optionally be resampled using the -step option. This argument is required unless -icbm_model is specified."],
	 ["-step", "float", 1, \$stepValue,
	  "Isostep size to use for the grid. Uses input sampling if unspecified."],
     ["-icbm_model", "float", 1, \$icbmModel,
      "Use one of the icbm models, where the required floating point value is the sampling size - which already has to exist in the ICBM model directory."],
	 );

    GetOptions(\@argTbl, \@ARGV, \@leftOverArgs) or die "\n";

    $grey = shift @leftOverArgs or die $usage;
    $white = shift @leftOverArgs or die $usage;
    $outputFile = shift @leftOverArgs or die $usage;
    
    # check if output exists and/or clobber was specified
    if (test_file('-e', $outputFile) && (! $Clobber)) {
	die "File $outputFile exists and -clobber was not specified\n";
    }

    # create the temporary volume
    $tmpVolume = "${TmpDir}/resampled_volume.mnc";
    check_output_dirs($TmpDir);

    AddDefaultArgs("mincresample", '-clobber');
    &self_announce if $Verbose;
}

sub createInfoText {
    $license = <<LICENSE;
Copyright Alan C. Evans
Professor of Neurology
McGill University
LICENSE

    $usage = <<USAGE;
Usage: $ProgramName [options] grey_surface.obj ... 
           white_surface.obj output_grid.mnc
       $ProgramName -help for details

$license
USAGE

$help = <<HELP;

No help text yet. Sorry.
HELP

Getopt::Tabular::SetHelp($help, $usage);

}


    
